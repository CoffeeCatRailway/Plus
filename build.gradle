plugins {
    id 'dev.architectury.loom' version '0.10.0-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.5.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.modName
version = "${project.mcVersion}-${project.modVersion}"
group = project.mavenGroup

loom {
    // use this if you are using the official mojang mappings
    // and want loom to stop warning you about their license
    silentMojangMappingsLicense()

    forge {
        mixinConfigs = ["${project.modId}.mixins.json"]

        dataGen {
            mod project.modId
        }

        runs {
            client {
                runDir "run"
            }
            server {
                runDir "run/server"
            }
        }
    }

    launches {
        data {
            arg "--existing", file("src/main/resources").absolutePath
        }
    }
}

configurations {
    shade
}

repositories {
    mavenLocal()
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Progwml6 (Just Enough Items)"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
	maven {
        name = "JustEnoughItems fallback"
        url = "https://modmaven.k-4u.nl"
    }
//    maven {
//        name = "JitPack"
//        url = "https://jitpack.io"
//    }
}

dependencies {
    // to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.mcVersion}"

    // choose what mappings you want to use here
    // leave this uncommented if you want to use mojang's official mappings
    mappings minecraft.officialMojangMappings()

    // uncomment this if you want to use yarn mappings
    // mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    // uncomment this if you want to use mcp mappings
    // mappings "de.oceanlabs.mcp:mcp_${project.mcp_channel}:${project.mcp_mappings}"

    // your forge dependency, this is **required** when using Forge Loom in forge mode!
    forge "net.minecraftforge:forge:${project.mcVersion}-${project.forgeVersion}"

    modCompileOnly "mezz.jei:jei-1.18.1:${jeiVersion}:api"
    modRuntime "mezz.jei:jei-1.18.1:${jeiVersion}"

    // These mods make life easier during runtime. They aren't strictly necessary

    modRuntime "curse.maven:shutupexperimentalsettings-407174:3544525"

//    modImplementation "com.github.Ocelot5836:Sonar:${project.sonarVersion}"
//    shade "com.github.Ocelot5836:Sonar:${project.sonarVersion}"
}

processResources {
    // define properties that can be used during resource processing
    inputs.property("name", project.modName)
    inputs.property("id", project.modId)
    inputs.property("version", project.modVersion)
    inputs.property("author", project.modAuthor)

    // this will replace the property "${version}" in your mods.toml
    // with the version you've defined in your gradle.properties
    filesMatching("META-INF/mods.toml") {
        expand 'name': project.modName, 'id': project.modId, 'version': project.modVersion, 'author': project.modAuthor
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'io.github.ocelot.sonar', "io.github.ocelot.${project.modId}.lib.sonar"
    classifier "shadow"
    exclude(["**/*.ase", "**/*.bbmodel"])
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = 17
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }

    // add some additional metadata to the jar manifest
    manifest {
        attributes([
                "Specification-Title"     : project.modName,
                "Specification-Vendor"    : project.modAuthor,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.modId,
                "Implementation-Version"  : archiveVersion,
                "Implementation-Vendor"   : project.modAuthor,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
